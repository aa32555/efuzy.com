%ZAA02GEDC ;;%AA UTILS;1.24;UTIL: POCKET CALC;14MAY91  12:32
        ;;Copyright (C) 1990,91 Patterson, Gray & Associates, Inc. ;
        ;;All rights reserved. ;
        D FUNC^ZAA02GDEV S tHI=ZAA02G("HI"),tLO=ZAA02G("LO"),ZAA02G("EON")=^%T("ECHO-ON"),ZAA02G("EOF")=^%T("ECHO-OFF"),DFL=987 D BEG(13,55) K DFL Q
BEG(TR,FC) N (FC,ZAA02G,TR,DFL,ZAA02GP,ZAA02GX,ZAA02GY,tHI,tLO,RESULT,REFRESH) X ZAA02G("EOF") S DF=$S($D(DFL)=0:0,DFL=987:1,1:0) D CUBE
        S %R=TR+2,%C=FC+4,PR=1,PC=1,ZAA02GQ="ZAA02GQ="_ZAA02GP,@ZAA02GQ,%C=%C+12,ZAA02GK="ZAA02GK="_ZAA02GP,@ZAA02GK,%R=TR+4,%C=FC+17,ZAA02GO="ZAA02GO="_ZAA02GP,@ZAA02GO,(AC,EN)="0.",(OOP,OP,ZAA02GT)="",DP=0,ZAA02GJ="$J(AC,12)" F I=1:1:4 S @("ZAA02G"_I)=0
        W tHI,ZAA02GQ,@ZAA02GJ F ZAA02GI=0:0 D READ Q:FK="EX"  D @$S(A?1N:"DIG",A=".":"DEC",FK="RU":"RUB",FK="CR":"CR","?CcFfMm"[A:"CMD","+-/*Xx"[A:"OP",",="[A:"EQ",A="%":"PCT",1:"ERR")
        S RESULT=AC,REFRESH=(TR-1)_":"_(TR+12) X ZAA02G("EON") Q
CMD G FNC^ZAA02GEDC1:"Ff"[A,CLR:"Cc"[A,MODE^ZAA02GEDC1:"Mm"[A,HELP^ZAA02GEDC1:A="?",ERR
READ S (A,FK)="" R A#1 I A="" X ZAA02G("T") S FK=$P(ZAA02GY,"\",$A(ZAA02GX,$F(ZAA02GX,ZF))+2)
        I $L(A),"Ee"[A S FK="EX"
        Q
CUBE S %R=TR,%C=FC,HL="",$P(HL,ZAA02G("HL"),19)="" W @ZAA02GP,tHI,ZAA02G("G1"),ZAA02G("TLC"),HL,ZAA02G("TRC"),"  "
        S %C=FC F %R=TR+1:1:TR+10 W @ZAA02GP,ZAA02G("VL"),$J("",18),ZAA02G("VL"),"  "
        S %R=%R+1,%C=FC W @ZAA02GP,ZAA02G("BLC"),HL,ZAA02G("BRC"),tLO
        S %R=TR-1,%C=FC+2 W @ZAA02GP,HL,ZAA02G("HL"),ZAA02G("TRC")
        S %C=FC+21 F %R=TR:1:TR+9 W @ZAA02GP,ZAA02G("VL")
        S %R=TR+1,%C=FC+2 W @ZAA02GP,ZAA02G("TLC"),$P(HL,ZAA02G("HL"),1,15),ZAA02G("TRC")
        S %R=%R+1 W @ZAA02GP,ZAA02G("VL"),$J("",14),ZAA02G("VL") S %R=%R+1 W @ZAA02GP,ZAA02G("BLC"),$P(HL,ZAA02G("HL"),1,15),ZAA02G("BRC"),ZAA02G("G0")
FACE S %R=TR+3,%C=FC+2 F I=1:1:7 S %R=%R+1,J=$P("POCKET CALC,,,,C-Clear  F-Funct,E-Exit   M-Mode,",",",I) W @ZAA02GP,J,$J("",16-$L(J))
        Q
DIG Q:$L(EN)>10  S:DP EN=EN_A S:'DP EN=EN*10+A_"." W ZAA02GQ,$J(EN,12) Q
DEC S DP=1 W ZAA02GQ,$J(EN,12) Q
CR S A="+"
OP S:"Xx"[A A="*" W ZAA02GO,A,ZAA02GK D:+EN COMPUTE S OP=A,OOP="" Q
PCT S ZAA02GT="/100" S:"+-"[OP ZAA02GT=ZAA02GT_"+AC",OP="*"_OP
EQ S:A="," A="=" W ZAA02GO,A,ZAA02GK I OOP]"" S OP=OOP,AC=ZAA02GLV
        E  S OOP=OP,ZAA02GLV=AC
        D:OP]"" COMPUTE S OP="" Q
ERR W *7 Q
COMPUTE I OP]"",ZAA02G1 D DISP1
        S:OP="" OP="+" I OP="/"&(+AC=0) S AC="error"
        E  S @("AC="_+AC_OP_(+EN)_ZAA02GT),ZAA02GT=""
        S:AC'["." AC=AC_"." D OVR:$L(AC)>12,DISP S DP=0,EN="0." Q
CLR W ZAA02GQ,$J("0.",12) I EN="0." S AC=0,(OP,OOP)="" W ZAA02GO,*32,ZAA02GK D:ZAA02G1 DISP1 Q
        S EN="0.",DP=0 Q
DISP W ZAA02GQ,@ZAA02GJ I ZAA02G1 D:PR_PC=11 ERA S %R=PR,%C=PC W @ZAA02GP,@ZAA02GJ W:"=%"[A "  ",A S PR=PR+1 S:PR=25 PR=1,PC=PC+15 S:PC>45 PC=1 W ZAA02GQ,@ZAA02GJ Q
        Q
DISP1 D:PR_PC=11 ERA S %R=PR,%C=PC W @ZAA02GP,$J(EN,12),"  ",OP S PR=PR+1 S:PR=25 PR=1,PC=PC+15 S:PC>45 PC=1
        Q
ERA S %C=1 F %R=1:1:24 W @ZAA02GP,$J("",54)
        Q
RUB Q:'$L(EN)  S:DP EN=$E(EN,1,$L(EN)-1) S:'DP EN=EN\10_"." W ZAA02GQ,$J(EN,12) Q
OVR S ZAA02GT=$L($P(AC,".",2)),ZAA02GT=ZAA02GT+12-$L(AC) I ZAA02GT>0 S AC=$J(AC,0,ZAA02GT-1)
        E  S AC="Over flow",OP="",EN=AC
        S ZAA02GT="" Q
        ;
