%ZAA02GEDOT ;;%AA UTILS;1.24;OTHER OPTIONS;14MAY91  12:28
        ;;Copyright (C) 1990,91 Patterson, Gray & Associates, Inc. ;
        ;;All rights reserved. ;
        D MENU,REST Q:X<2  D BEG(4,rm-70):X=2,BEG^ZAA02GEDC(10,rm-27):X=3,REST S D=$D(@gl@(s)) K D Q
MENU N (X,ZAA02G,ZAA02GP,ZAA02GX,ZAA02GY,REFRESH) S Y="25,5\RLHD\1\OTHER OPTIONS\\Return        ,ASCII Chart   ,Calculator    ",(REFRESH,X)="" X ZAA02G("EOF") D ^ZAA02GEDPO X ZAA02G("EON") Q
REST D RESTORE^ZAA02GED08(+REFRESH,$P(REFRESH,":",2)) Q
BEG(TR,FC) N (FC,ZAA02G,TR,ZAA02GP,REFRESH) S HMI=8,VMI=(128/HMI)+.9\1,WD=HMI*8-1
        S HL="",$P(HL,ZAA02G("HL"),WD+3)="",G1=ZAA02G("G1"),G0=ZAA02G("G0"),VL=G1_ZAA02G("VL")_G0,HI=ZAA02G("HI"),LO=ZAA02G("LO")
        S %R=TR,%C=FC W @ZAA02GP,LO,G1,ZAA02G("TLC"),HL,ZAA02G("TRC"),G0 F C=0:1:VMI-1 D LINE
        S %R=%R+1,%C=FC W @ZAA02GP,LO,G1,ZAA02G("BLC"),HL,ZAA02G("BRC"),G0
        S %C=FC+(WD\2)-6 W @ZAA02GP,"Press RETURN " R *C
        S REFRESH=TR_":"_%R Q
LINE S %R=%R+1,%C=FC W @ZAA02GP,VL
        F I=C:VMI Q:I>127  W LO,$J(I,3),") ",HI,$S(I=127:"RUB",I>31:$C(I)_"  ",1:$P("NUL,SOH,STX,ETX,EOT,ENQ,ACK,BEL,BS ,HT ,LF ,VT ,FF ,CR ,SO ,SI ,DLE,DC1,DC2,DC3,DC4,NAK,SYN,ETB,CAN,EM ,SUB,ESC,FS ,GS ,RS ,US ",",",I+1))
        S %C=FC+WD+2 W @ZAA02GP,LO," ",VL Q
        ;
