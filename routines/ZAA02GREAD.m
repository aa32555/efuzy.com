ZAA02GREAD 
SETUP K ^ZAA02GREAD S ZAA02G("OS")=^ZAA02G("OS"),A=$P($T(READ)," ",2,99),^ZAA02GREAD=A
 I ZAA02G("OS")="DTM" S ^ZAA02GREAD=$ZXECUTE(A) F I=1:1 S A=$T(READ+I) Q:A=""  S B=$P(A," ",2,255),A=$P(A," "),^ZAA02GREAD(A)=$ZXECUTE(B)
 I ZAA02G("OS")'="DTM" F I=1:1 S A=$T(READ+I) Q:A=""  S B=$P(A," ",2,255),A=$P(A," "),^ZAA02GREAD(A)=B
 K A,B,I Q
 ;
TEST D:'$D(ZAA02G) INIT^ZAA02GDEV S ZAA02GCC=1,X="123456789012345678901234567890"
T1 S PROMPT="X=",%R=24,%C=37,LNG=30,RX=0,TRM="Q",FNC=^ZAA02G(.3) W @ZAA02GP,"|" S %C=5 X ^ZAA02GREAD W !!,X,"  RX=",RX,"  ZAA02GCC=",ZAA02GCC,"  FNC=",FNC,!! G T1
 ;
READ D:$D(ZAA02G)=0 INIT^ZAA02GDEV N ZAA02Gi,ZAA02Gj S ZAA02Gi=0 F ZAA02Gj=0:0 X ^ZAA02GREAD(ZAA02Gi) Q:ZAA02Gi=99
0 S ZAA02Gi=10,ZAA02Gl=$S($D(ZAA02GCC):ZAA02GCC,1:1),ESC=0,ZAA02Gf="" S:$D(FNC) ZAA02Gf=FNC,FNC="" X:$D(PROMPT) ^(2) S:$D(LNG)=0 LNG=79-%C S:ZAA02Gl>LNG ZAA02Gl=LNG
1 S ZAA02Gi=3 F ZAA02Gj=0:0 R ZAA02GC#1:TIM S:'$T ZAA02Gi=16 Q:ZAA02GC=""  S:$D(UC) ZAA02GC=$S(ZAA02GC?1L:$C($A(ZAA02GC)-32),1:ZAA02GC) S:$D(TRM) ESC=TRM[ZAA02GC S:ESC ZAA02Gi="CR" S:CHR'=""&(CHR'[ZAA02GC) ZAA02Gi=7 Q:ZAA02Gi'=3  S:ZAA02Gl'<LNG ZAA02Gi=6 Q:ZAA02Gi'=3  W ZAA02GC S X=$E(X,1,ZAA02Gl-1)_ZAA02GC_$E(X,ZAA02Gl+1,255),ZAA02Gl=ZAA02Gl+1
2 W @ZAA02GP,ZAA02G("LO"),PROMPT,ZAA02G("HI"),X S %C=%C+$L(PROMPT)
3 X ZAA02G("T") S ZF=$P(^ZAA02G(.3),"\",$A(^ZAA02G(.3,ZAA02G,0),$F(^(0),ZF))+2),ZAA02Gi=$S(ZF="":7,ZAA02Gf[ZF:17,$D(^ZAA02GREAD(ZF)):ZF,1:7)
RK X $S(ZAA02Gl<LNG:"W ZAA02G(""RT"") S ZAA02Gl=ZAA02Gl+1,ZAA02Gi=1",$D(RX):"S RX=5,ZAA02Gi=""CR""",1:"S ZAA02Gi=1")
LK X $S(ZAA02Gl>1:"S ZAA02Gl=ZAA02Gl-1,ZAA02Gi=1 W ZAA02G(""L"")",$D(RX):"S RX=4,ZAA02Gi=""CR""",1:"S ZAA02Gi=1")
6 S:ZAA02Gl>LNG ZAA02Gi=7 Q:ZAA02Gi=7  W ZAA02GC S X=$E(X,1,ZAA02Gl-1)_ZAA02GC_$E(X,ZAA02Gl+1,255),ZAA02Gl=ZAA02Gl+1,ZAA02Gi=$S($D(AUTO)#2=0:1,AUTO:"CR",1:1)
7 W *7 S ZAA02Gi=1
DC W $E(X,ZAA02Gl+1,256)," " S X=$E(X,1,ZAA02Gl-1)_$E(X,ZAA02Gl+1,256)_" ",ZAA02Gi=1 W @ZAA02GP,$E(X,1,ZAA02Gl-1)
RU S ZAA02Gi=1 I ZAA02Gl>1 S X=$E(X,1,ZAA02Gl-2)_" "_$E(X,ZAA02Gl,256),ZAA02Gl=ZAA02Gl-1 W *8," ",*8
10 S ZAA02Gi=1,ZAA02Gc=%C,%C=%C+ZAA02Gl-1 W @ZAA02GP,ZAA02G("HI") S %C=ZAA02Gc,X=X_$J("",LNG-$L(X)),TIM=$S($D(TIM):TIM,1:99999) X ^ZAA02G("TERM-ON"),^ZAA02G("WRAP-OFF"),^ZAA02G("ECHO-OFF") S:$D(CHR)=0 CHR="" S:$D(RX) RX=0
EF S X=$E(X,0,ZAA02Gl-1)_$J("",LNG-ZAA02Gl+1),ZAA02Gc=%C,%C=%C+ZAA02Gl-1,ZAA02Gi=1 W @ZAA02GP,$J("",LNG-ZAA02Gl+1),@ZAA02GP S %C=ZAA02Gc K ZAA02Gc
CR S ZAA02Gc=X,ZAA02Gi=13 S:ESC RX=ZAA02GC I $E(X,$L(X))=" " F ZAA02Gj=0:0 S ZAA02Gj=$F(X," ",ZAA02Gj) Q:'ZAA02Gj  I $E(X,ZAA02Gj,255)?." " S X=$E(X,1,ZAA02Gj-2) Q
13 S:X="" ZAA02Gi=20 I X'="" X:$D(PAT) "I $L(PAT) S ZAA02Gi=19 I @PAT S ZAA02Gi=13" X:ZAA02Gi=13 "I $D(MAX),X>MAX S ZAA02Gi=19" X:ZAA02Gi=13 "I $D(MIN),X<MIN S ZAA02Gi=19" S:ZAA02Gi=13 ZAA02Gi=20
IC X $S($E(X,LNG)'=" ":"W *7",1:"W "" "",$E(X,ZAA02Gl,LNG-1) S X=$E(X,1,ZAA02Gl-1)_"" ""_$E(X,ZAA02Gl,LNG-1) W @ZAA02GP,$E(X,1,ZAA02Gl-1)") S ZAA02Gi=1
UK S ZAA02Gi="CR" S @$S(ZAA02Gf[ZF:"ZAA02Gf=ZF",$D(RX):"RX=1",1:"ZAA02Gi=7")
DK S ZAA02Gi="CR" S @$S(ZAA02Gf[ZF:"ZAA02Gf=ZF",$D(RX):"RX=2",1:"ZAA02Gi=7")
TB S ZAA02Gi="CR" S @$S(ZAA02Gf[ZF:"ZAA02Gf=ZF",$D(RX):"RX=3",1:"ZAA02Gi=7")
16 S RX=-1,ZAA02Gi=20
17 S FNC=ZF,ZAA02Gi="CR"
19 S X=ZAA02Gc,(ZAA02Gi,ZAA02Gl)=1,ESC=0 S:$D(RX) RX=0 W *7,@ZAA02GP
20 X ^ZAA02G("ECHO-ON"),^ZAA02G("TERM-OFF"),^ZAA02G("WRAP-ON") S:$D(ZAA02GCC) ZAA02GCC=ZAA02Gl K PROMPT,MAX,MIN,LNG,CHR,PAT,TRM,UC,ZF,TIM,ZAA02Gj,ZAA02Gc,ZAA02Gl,ZAA02Gf S ZAA02Gi=99
