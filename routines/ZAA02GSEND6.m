ZAA02GSEND6 ;PG&A,ZAA02G-SEND,1.36,MODEM TRANSFER (SCRIPT CONTROL);29DEC95 3:03P;;;09FEB98  09:55
 ;PG&A, 1984
 ;
RUN I $O(@ZAA02GSENDG@("SCR",""))="" W !!,"NO SCRIPTS FOUND - PRESS RETURN" R A#1 Q
 D EDT1^ZAA02GSEND7,EDS^ZAA02GSEND7 I X'="",$D(YY(X))  S SCR=YY(X) K YY,%1,%2,%3,%4,%5 S %IN=1 D RUNX
 K YY,%IN,QT Q
RUNX D:'$D(ZAA02G) INIT^ZAA02GDEV D ZAA02GSINT I '$D(LTERMON) S LOS=^ZAA02G("OS") D OSS^ZAA02GSEND2
 S:'$D(QT) QT=1 W:QT ZAA02G("F"),!!,ZAA02G("RON")," SCRIPT MODE - Press Control ",$C($S($D(TR):$A(TR),1:20)+64)," to exit ",ZAA02G("ROF"),!! D RUNQ I QT W !!,ZAA02G("RON")," Exit Script Mode - Press RETURN ",ZAA02G("ROF") R A#1
 Q
RUNQ S CNT=10,DELAY=0,TRACE=0,(C,ST)="",ER=0 S:'$D(QT) QT=1 K CPI,FILTER S:'$D(MODEM) MODEM=0,MFL=0
 S:$D(XT)=0 XT="" S:$D(TR)=0 TR=$C(20) U MODEM I '$D(TIM) D TIME
 D O0 U:QT 0 I $D(MFL) C MODEM K MFL
 Q
O0 D TERMON^ZAA02GSEND1
ODNLD D O3 I $D(WPDN) D DNLD
 D TERMOF^ZAA02GSEND1 K D,DV,ZAA02G("TR"),CNT,DELAY,TRACE,ST,TR,XT,LAB,SCR,TRM,FILA,FILTER,CPZ Q
O3 S SCRL=.03 K LAB F I=1:1 S L=SCRL,SCRL=$O(@ZAA02GSENDG@("SCR",SCR,SCRL)) Q:SCRL=""  I ^(SCRL)?1.AN1":" S LAB($P(^(SCRL),":"))=L
 S SCRL=.03 F I=1:1 S SCRL=$O(@ZAA02GSENDG@("SCR",SCR,SCRL)) Q:SCRL=""  S TXT=^(SCRL) I TXT'="" I TXT'?1.AN1":",$E(TXT,1,3)'="REM",$E(TXT)'=";" D O4 Q:SCRL=999999
 Q
O4 U:QT 0 S A=$TR($P(TXT,"-"),"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ"),B=$P(TXT,"-",2,99) W:TRACE !,"*** ",TXT,! I A'="RUN-",B["%" F J=1:1:$L(B,"%") F K=1:1:9 S L="%"_K I B[L D:'$D(@L) %R S B=$P(B,L)_@L_$P(B,L,2,99)
 D:B["\" PARSE
 I $E(A)="S" S K="O41,SEND,O43,SET WAIT,O51,SET DELAY,O63,SET FILTER,O64,SET BAUD,O65,SET DEVICE,O66,SET FLAG,O53,SKERMIT,O56,SASCII,O69,SGLOBAL,O71,SROUTINE," I K[(","_A_",") G @$P(K,",",$L($P(K,","_A_","),","))
 S K="O42,WAIT FOR,O44,CAPTURE ON,O45,CAPTURE OFF,O46,TRACE ON,O47,TRACE OFF,O48,IF GOTO,O49,ELSE GOTO,O50,GOTO,O52,X,O59,END,O60,RUN,O61,REPORT,O1,TERM ON,O62,QUIET ON,O62A,QUIET OFF," I K[(","_A_",") G @$P(K,",",$L($P(K,","_A_","),","))
 S K="O54,RKERMIT,O55,CLEAR,O57,RASCII,O67,PAUSE,O68,OPTION,O70,RGLOBAL," I K[(","_A_",") G @$P(K,",",$L($P(K,","_A_","),","))
E W:QT "INVALID SCRIPT LINE - ",TXT,!,*7 H 3 Q
%R I '$D(@L),$D(%IN) U 0 W !,"INPUT ",L,"-" X ^ZAA02G("ECHO-ON") R @L,! X ^ZAA02G("ECHO-OFF")
 Q
O41 H DELAY U MODEM W B S C="" Q
O42 S ER=1 S:B="" B=$C(1)
 F K=1:0:CNT U MODEM R A:1 S:'$T K=K+1 X TRM S E=A X XT S A="" X:QT "U 0 W E R A:0 X T" Q:A[TR  S C=$S($L(C)+$L(E)<255:C_E,$L(E)>100:E,1:$E(C,200,255)_E) F KK=1:1:$L(B,"|") I C[$P(B,"|",KK) S K=CNT,ER=0 S:B["|" ER=0_","_KK S C=$P(C,$P(B,"|",KK),2,99) Q
O42E I ER,A[TR S SCRL=999999,ER=0 W:QT !,"<<<ESCAPE>>>",!
 Q
O43 S:B?1.N CNT=B Q
O46 S TRACE=1 Q
O47 S TRACE=0 Q
O48 G:'ER O50 Q
O49 G:ER O50 Q
O50 S B=$TR($S(ER[","&(B["|"):$P(B,"|",$P(ER,",",2)),1:B),":","") I B]"",$D(LAB(B)) S SCRL=LAB(B) Q
 Q
O51 S:B?1.N DELAY=B Q
O52 X B Q
O53 G SKERMIT^ZAA02GSENDA
O54 G RKERMIT^ZAA02GSENDA
O55 W:$D(ZAA02G("F")) ZAA02G("F") Q
O56 G SASCII^ZAA02GSEND8
O57 G RASCII^ZAA02GSEND8
O59 S SCRL=99999 Q
O60 D O601 Q
O601 D O603 S:SSCRL=999999 SCRL=SSCRL K SSCRL Q
O603 N SCR,SCRL,LAB,%1,%2,%3,%4,%5 S SCR=$P(B," ") D:$P(B," ",2)'="" O602 D O3 S SSCRL=SCRL Q
O602 S B=$P(B," ",2,99) F J=1:1:$L(B,",") S @("%"_J)=$P(B,",",J)
 Q
O61 Q:'QT  U 0 W !,"<<<",B,">>>",! Q
O62 S QT=0 Q
O62A S QT=1 Q
O63 S A=$S(B[":":":",B[",":",",1:"") Q:A=""  S FILTER=$P(B,A),FILA=$P(B,A,2) Q
O64 D BAUD^ZAA02GSEND7 Q
O65 Q:B=""  S D=B I D?1.N1"."1.N1"."1.N1"."1.N S ip=D,port=23 D IP^ZAA02GSEND1 S (D,DV)=MODEM D TERMON^ZAA02GSEND1 Q
 O D::0 E  W !,"<<<DEVICE NOT AVAILABLE>>>",! S SCRL=999999 Q
 S (MODEM,DV)=D,A=ZAA02G(1),ZAA02G(1)=0 D LON^ZAA02GSEND1 Q
O66 S:B?1.N ER=B Q
O67 H +B Q
O68 S ER=1 F K=1:0:CNT U MODEM R A:1 S:'$T K=K+1 X TRM S E=A X XT S A="" X:QT "U 0 W E R A:0 X T" Q:A[TR  S C=$S($L(C)+$L(E)<255:C_E,$L(E)>100:E,1:$E(C,200,255)_E) I C[B S ER=0 Q
 I 'ER S C=$P(C,B) F K=$L(C):-1:1 I $E(C,K)=" " F K=K-1:-1:1 I $E(C,K)'=" " H DELAY U MODEM W $E(C,K),*13 S C="" U:QT 0 G O42E
 G O42E
O69 D SGLOBAL^ZAA02GSEND7 Q
O70 D RGLOBAL^ZAA02GSEND7 Q
O71 D SROUTINE^ZAA02GSEND7 Q
PARSE I B?.E1"\"3N.E F J=2:1:$L(B,"\") I $P(B,"\",J)?3N.E S B=$P(B,"\",1,J-1)_$C($E($P(B,"\",J),1,3))_$E($P(B,"\",J,99),4,255) G PARSE
 S B=$TR(B,"\",$C(13)) Q
O1 F I=1:1 D O2 Q:A[TR
 Q
O2 U MODEM R A:0 X TRM S B=A U 0 W A X XT R A:1 X T G OESC:A[TR Q:A_B=""  U MODEM W A
 F J=1:1:TIM U 0 R A:0 X T H 0 Q:A[TR  U MODEM W A S:A'="" J=1 R A:0 X TRM H 0 X XT U 0 W A S:A'="" J=1
 G:A'[TR O2
OESC Q:'QT  U 0 W !,"<<<ESCAPE>>>",! Q
O45 Q:'$D(CP)  I $D(CPX) S CPZ=CPX D CPX2
 K CP,CPX S XT=$P(XT,"D CPX")_$P(XT,"D CPX",2) Q
O44 U:QT 0 S CP="@ZAA02GSENDG@(""XFR"","""_$I_""")" S:B]"" CP=B I '$D(CPI) U:QT 0 K @CP S CPI=10,CPC="" F J=0:2:31 S CPC=CPC_$C(J,J+1)
 S:XT'["CPX" XT=XT_$S(XT]"":" ",1:"")_"D CPX",CPX="" Q
CPX I $L(CPX)+$L(A)>250 S CPZ=CPX_$E(A,1,250-$L(CPX)),A=$E(A,250-$L(CPX)+1,255),CPX=CPZ D CPX1 I $L(CPX)>200 S CPZ=CPX,CPX="" D CPX2
 S CPX=CPX_A
CPX1 I CPX[$C(13,10) S CPZ=$P(CPX,$C(13,10)),CPX=$P(CPX,$C(13,10),2,255) D CPX2 G CPX1
 Q
CPX2 I $D(FILTER),CPZ[FILTER F J=1:1:$L(CPZ,FILTER)-1 S CPZ=$P(CPZ,FILTER)_FILA_$P(CPX,FILTER,2,99)
 S @CP@(CPI)=$TR(CPZ,CPC,""),CPI=CPI+1 Q
 ;
TIME S A=$H F J=1:1 Q:$H'=A
 S A=$H F J=1:1 R B:0 R B:0 Q:A'=$H
 S TIM=J*20 Q
 ;
ZAA02GSINT S ZAA02GSENDG=$S($D(^%ZAA02GSEND)#2:^%ZAA02GSEND,1:"^ZAA02GSEND") Q
WP ; MACRO ENTRY - #S SCR="X" G WP^ZAA02GSEND6
 S REFRESH=1,KK=0,A=.03 F J=1:1 S A=$O(@ZAA02GWPD@(A)) Q:A=""  I $P(^(A),XF,4)'?." " S WPDN=A
 D ZAA02GSINT,WP1 S MC="" Q:'KK
 U 0 K SVSB S ZAA02GINS="@ZAA02GSENDG@(""XFR"","""_$I_""")"
 S A="",OMG=MG F J=1:1 S A=$O(@ZAA02GINS@(A)) Q:A=""  I $L(^(A))>MG S MG=$L(^(A))
 I OMG'=MG S:MG>128 MG=128 S RM=MG,(TP,ML)=1,@ZAA02GWPD@(.01)=MG F I=.001:.001:.009 I $D(^(I)) S ^(I)=$E(^(I)_$J("",130),1,MG+2)
 I  S TB=^(.001)
 S IMP="",SXFM=XFM,SXSB=SB,KK=KK+1 D INSRT^ZAA02GWPV0 S SB=SXSB S:'$D(@ZAA02GWPD@(SB)) SB=TOP,CR=TL K SXFM,SXSB,IMP,SVSB Q
 Q
WP1 N (ZAA02GSENDG,WPDN,ZAA02G,ZAA02GP,KK,SCR,ZAA02GWPD,DIR,DOC) D @$S($D(SCR):"RUNX",1:"RUN") S KK=$S($G(CPI):CPI-10,1:0) K SCR U 0 S A=ZAA02G(1),ZAA02G(1)=0 X ^ZAA02G("TERM-ON") S ZAA02G(1)=A Q
DNLD U 0 W !!,"Press Q to quit, D to download current document - " R A#1 S:A?1L A=$C($A(A)-32) W A,! I A="D" D DNL1
 Q
DNL1 W "wait while downloading..."
 U MODEM S A=.03 F J=1:1 S A=$O(@ZAA02GWPD@(A)) Q:A=""  W $P(^(A),$C(1),4),*13 R B:0 Q:WPDN=A
 ; F J=1:1:J R B:1 E  Q
 U 0 W "done",!!,"<<TERMINAL MODE>>",! U MODEM D O1 Q
ZAA02GSEND7 ;PG&A,ZAA02G-SEND,1.36,MODEM TRANSFER (SCRIPT CONTROL);29SEP95 12:30P;;;27JAN98  20:52
 ;PG&A, 1984
EDIT S %R=3,%C=1 W @ZAA02GP,ZAA02G("CS")
 D EDT1 S Y(Y+1)="(new entry)",YY(Y+1)="" D EDS G END:X="",END:'$D(YY(X)) S SCR=YY(X) I SCR="" D EDTC G:SCR="" END S @ZAA02GSENDG@("SCR",SCR)=X
 S %R=4,%C=36-($L(SCR)\2) W @ZAA02GP,"SCRIPT: ",SCR
 S TL=7,BL=22,WO="",MG=60,ZAA02GWPD="@ZAA02GSENDG@(""SCR"",SCR)",NM=SCR,ZAA02GWPOPT="ST,OT2,5,8,9,18",HLPR="^ZAA02GSENDH"
 D ENTRY^ZAA02GWPV6 S A=.03 K Y,XX,HLPR S Y=0 F J=1:1 S A=$O(@ZAA02GSENDG@("SCR",SCR,A)) Q:A=""  S B=^(A) I $E(B,1,3)'="REM",$E(B)'=";" D:B'="" CHK
 I J=1 K @ZAA02GSENDG@("SCR",SCR) G END
 I $D(Y)>1 S %R=6,%C=1 W @ZAA02GP,Y," ERROR",$S(Y>1:"S",1:"")," ENCOUNTERED IN SCRIPT",!!,"LINE",?10,"COMMAND",?50,"ERROR",!,"------    ------------------------------------    ---------------------",!
 I  S A="" F J=1:1 S A=$O(Y(A)) Q:A=""  W A,?10,$E($P(Y(A),$C(0)),1,38),?50,$P(Y(A),$C(0),2),!
 I  W !,"PRESS RETURN" R C#1
END G T4^ZAA02GSEND1
 ;
CHK S C=$TR($P(B,"-"),"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ"),K=0 I B["-"
 I  F K=1:1:26 I $P("SEND,WAIT FOR,SET WAIT,SET DELAY,SET BAUD,GOTO,ELSE GOTO,IF GOTO,RUN,REPORT,SET DEVICE,SET FILTER,SET DEVICE,SET FLAG,SKERMIT,RKERMIT,SASCII,RASCII,PAUSE,CAPTURE ON,OPTION,SGLOBAL,RGLOBAL,SROUTINE,X",",",K)=C Q
 I K=26 S Y(J)=B_$C(0)_"INVALID COMMAND",Y=Y+1 Q
 Q:K  I B?1.AN1":" Q
 X "F K=1:1:19 I $P(""TERM ON,CAPTURE ON,CAPTURE OFF,END,TRACE ON,TRACE OFF,QUIET ON,QUIET OFF,CLEAR,X,X,X,X,X,X,X,X,X,X,X,X,X"","","",K)=C Q" S:K=19 Y(J)=B_$C(0)_"INVALID COMMAND",Y=Y+1 Q
 Q
EDS S Y="20,8\RHLY\1\ZAA02G-SEND SCRIPT LIST",Y(0)="\EX" D ^ZAA02GPOP S:X["EX" X="" K Y Q
 ;
EDT1 K Y S Y=0,A="" F J=1:1 S A=$O(@ZAA02GSENDG@("SCR",A)) Q:A=""  S Y=Y+1,Y(Y)=A_$J("",20-$L(A))_^(A),YY(Y)=A
 Q
EDTC S Y="25,12\LHD\1\ SCRIPT DEFINITION *\\*,Enter Script Name - *            ",X="" K LNG,PAT,TRM,CHR D ^ZAA02GPOP S SCR=X Q:X=""
 S Y="20,14\RLH\1\ SCRIPT DESCRIPTION *\\*,Enter short description - *                                   ",X="" D ^ZAA02GPOP Q
 ;
BAUD ; B - baud,parity,data bits
 S DV=MODEM I ^ZAA02G("OS")="MSM" U DV:(:::::::$S(B["N":2,B["O":5,1:9)*4096+($S($P(B,",",3)=7:5,1:9)*16)+$S(+B=300:7,+B=1200:9,+B=2400:11,+B=4800:12,+B=19200:14,+B=38400:15,1:13)) U 0 Q
 I ^ZAA02G("OS")="DTM" U DV:(SPEED=+B:PARITY=$S(B["N":"NONE",B["O":"ODD",1:"EVEN"):CHARBITS=$P(B,",",3)) U 0 Q
 I ^ZAA02G("OS")="PSM" S A=$S(+B=300:5,+B=1200:7,+B=2400:10,+B=4800:11,+B=19200:14,+B=38400:15,1:13) U DV:(:::A*16+A*8+$S(B["N":0,B["E":3,1:1)*4+$S($P(B,",",3)=7:2,1:3)*8) U 0 Q
 Q
CBAUD S Y="25,13\RLHW\1\ BAUD RATE CHANGES *\\*,  Speed      Parity     Data Bits   *,*,*,*, - Use Cursor Keys to change*, - Press SELECT key to execute change*, - Press EXIT to quit without changes",X=""
 D ^ZAA02GPOP W ZAA02G("RON") S %R=17,%C=30 W @ZAA02GP,"9600         N           8"
 S ZF=33,SP=6,PR=3,DT=2 F JJ=1:0:3 D:JJ=1 CSPEED D:JJ=2 CPAR D:JJ=3 CDATA S JJ=$S(ZF=35:JJ+1,ZF=57:4,ZF=36&(JJ=1):JJ,ZF=59:4,1:JJ-1)
 W ZAA02G("ROF") S %R=13,%C=1 W @ZAA02GP,ZAA02G("CS") I ZF'=57 S B=$P("300,600,1200,2400,4800,9600,19200,38400",",",SP)_","_$E("OEN",PR)_","_$E(78,DT) D BAUD
 K JJ,PAT,SP,PR,DT Q
CSPEED S %C=30,X=SP,PAT="300  ,600  ,1200  ,2400 ,4800 ,9600 ,19200,38400" D CCHG S SP=X Q
CPAR S %C=43,X=PR,PAT="O,E,N" D CCHG S PR=X Q
CDATA S %C=55,X=DT,PAT="7,8" D CCHG S DT=X Q
CCHG X ^ZAA02G("TERM-ON"),^ZAA02G("ECHO-OFF") W ZAA02G("HI") F J=1:1 W @ZAA02GP,$P(PAT,",",X) R A#1 I A="" X ZAA02G("T") S ZF=$A(^ZAA02G(.3,ZAA02G,0),$F(^(0),ZF)) S X=X+$S(ZF=33:1,X'>1:0,ZF=34:-1,1:0) S:$P(PAT,",",X)="" X=X-1 Q:"35,36,57,59"[ZF
 X ^ZAA02G("ECHO-ON"),^ZAA02G("TERM-OFF") Q
 ;
SGLOBAL S SGLOBAL=B D OS^ZAA02GSEND S TYPE="G",DIRECT="L" U 0 G CONNECT^ZAA02GSEND
RGLOBAL S RGLOBAL=B D OS^ZAA02GSEND S TYPE="G",DIRECT="R" U 0 G CONNECT^ZAA02GSEND
SROUTINE F M=1:1:$L(B,",") S JJ=$P(B,",",M) S:JJ]"" RESTART(JJ)=""
 S RESTART="S UTIL=""RESTART""" D OS^ZAA02GSEND S TYPE="R",DIRECT="L",(NM,RT)="" U 0 G CONNECT^ZAA02GSEND
